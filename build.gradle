buildscript {
    apply from: 'project.gradle', to: buildscript
    copy {
        from zipTree(buildscript.configurations.classpath.files.find { it.name.contains('gradle-project-plugin') })
        into 'tmp'
        include 'gradle-scripts/**'
    }
}
apply from: 'tmp/gradle-scripts/_root.gradle'
/////////////////////////////////////////////
repositories {
    maven { url 'https://nexus.yamoney.ru/repository/gradle-plugins/' }
}

pluginId = 'yamoney-gradle-project-plugin'
gradlePlugin {
    plugins {
        gradleProjectPlugin {
            id = project.ext.pluginId
            implementationClass = "ru.yandex.money.gradle.plugins.gradleproject.GradleProjectPlugin"
        }
    }
}

jar {
    from(projectDir.absolutePath) {
        include "gradle-scripts/**/*"
        into "/"
    }
}

dependencies {
    compile localGroovy(),
            'org.ajoberstar:gradle-git:1.5.0',
            'ru.yandex.money.gradle.plugins:yamoney-check-dependencies-plugin:4.4.2',
            'ru.yandex.money.gradle.plugins:yamoney-git-expired-branch-plugin:2.0.4',
            'gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:0.5',
            'ru.yandex.money.gradle.plugins:yamoney-artifact-release-plugin:1.2.1'

    testCompile gradleTestKit(),
            'com.netflix.nebula:nebula-test:7.1.0'

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}