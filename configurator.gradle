repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'ru.yoomoney.gradle.plugins.java-artifact-publish-plugin'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'ru.yoomoney.gradle.plugins.artifact-release-plugin'

releaseSettings {
    releaseTasks = ["build", "publish", "closeAndReleaseMavenStagingRepository", "publishPlugins"]
    pathToGitPrivateSshKey = System.getenv("GIT_PRIVATE_SSH_KEY_PATH")
    passphraseToGitPrivateSshKey = System.getenv("GIT_KEY_PASSPHRASE")
    gitUsername = "yoomoney-robot"
    gitEmail = "SvcReleaserBackend@yoomoney.ru"
    changelogRequired = true
    addPullRequestLinkToChangelog = true
    pullRequestInfoProvider = "GitHub"
    githubAccessToken = System.getenv("GITHUB_TOKEN")
}

javaArtifactPublishSettings {
    nexusUser = System.getenv("NEXUS_USER")
    nexusPassword = System.getenv("NEXUS_PASSWORD")
    artifactId = 'gradle-project-plugin'
    groupId = 'ru.yoomoney.gradle.plugins'
    snapshotRepository = "https://oss.sonatype.org/content/repositories/snapshots/"
    signing = true
    staging {
        enabled = true
        nexusUrl = "https://oss.sonatype.org/service/local/"
    }
    publicationAdditionalInfo {
        addInfo = true
        organizationUrl = "https://github.com/yoomoney"
        license {
            name = "MIT License"
            url = "http://www.opensource.org/licenses/mit-license.php"
        }
        developers {
            developer {
                name = 'Oleg Kandaurov'
                email = 'kandaurov@yoomoney.ru'
                organization = 'YooMoney'
                organizationUrl = 'https://yoomoney.ru'
            }
        }
        description = "Gradle project plugin"
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava {
    options.compilerArgs.addAll(['--release', '8'])
}

jar {
    manifest {
        attributes(
                'Implementation-Version': "$project.version",
                'Implementation-Title': "$project.name",
                'Built-By': java.lang.System.getProperty('user.name'),
                'Built-Date': new Date().format('yyyy-MM-dd'),
                'Copyright': '2021 NBCO YooMoney LLC',
                'License': 'MIT'
        )
    }

    from(projectDir.absolutePath) {
        include "CHANGELOG.md", "README.md"
        into "META-INF"
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

test {
    systemProperty "file.encoding", "UTF-8"
    testLogging {
        events "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

apply plugin: "jacoco"
jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}
check.dependsOn jacocoTestReport

// создаем таску checkCoverage. В этом плагине она ничего не проверяет, но нужна для поддержки общего скрипта сборки
// для yoomoney. см. yoomoney/travis-shared-configuration:build-and-publish-plugin.yml
task checkCoverage {
    dependsOn jacocoTestReport
}

// создаем таску checkSnapshotsDependencies. В этом плагине она ничего не проверяет, но нужна для поддержки общего скрипта сборки
// для yoomoney. см. yoomoney/travis-shared-configuration:build-and-publish-plugin.yml
task checkSnapshotsDependencies {
    dependsOn jacocoTestReport
}

group = 'ru.yoomoney.gradle.plugins'

gradlePlugin {
    plugins {
        gradleProjectPlugin {
            id = 'ru.yoomoney.gradle.plugins.gradle-project-plugin'
            implementationClass = 'ru.yoomoney.gradle.plugins.gradleproject.GradleProjectPlugin'
            displayName = 'Gradle Project Plugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/yoomoney/gradle-project-plugin'
    vcsUrl = 'https://github.com/yoomoney/gradle-project-plugin.git'
    description = 'Gradle project plugin'
    tags = ['gradle', 'plugin', 'yoomoney']

    mavenCoordinates {
        groupId = "ru.yoomoney.gradle.plugins"
        artifactId = "gradle-project-plugin"
    }
}

project.getExtensions().getByType(GradlePluginDevelopmentExtension.class).setAutomatedPublishing(false)

